apply plugin: 'maven-publish'

publishing {
    repositories {
        maven {
            url "${System.getProperty('user.home')}/releases/maven/development"
        }
        maven {
            url "${System.getProperty('user.home')}/releases/maven/release"
        }
    }
}

def releaseNumber = 5

def pubVersion = "1.8.0-$releaseNumber-4e4df22"

def baseArtifactId = 'googletest'
def artifactGroupId = 'edu.wpi.first.thirdparty.frc2019'
def zipBaseName = '_GROUP_edu_wpi_first_thirdparty_frc2019_ID_googletest_CLS'

def outputsFolder = file("$project.buildDir/outputs")

def versionFile = file("$outputsFolder/version.txt")

task outputVersions() {
    description = 'Prints the versions of this to a file for use by the downstream packaging project'
    group = 'Build'
    outputs.files(versionFile)

    doFirst {
        buildDir.mkdir()
        outputsFolder.mkdir()
    }

    doLast {
        versionFile.write pubVersion
    }
}

build.dependsOn outputVersions
copyAllOutputs.dependsOn outputVersions
copyAllOutputs.inputs.file versionFile
copyAllOutputs.from versionFile

task cppSourcesZip(type: Zip) {
    destinationDir = outputsFolder
    baseName = zipBaseName
    classifier = "sources"

    from(licenseFile) {
        into '/'
    }

    from('googletest/googletest/src') {
        exclude '**/*-all.cc'
        exclude '**/*_main.cc'
        into '/'
    }

    from('googletest/googlemock/src') {
        exclude '**/*-all.cc'
        exclude '**/*_main.cc'
        into '/'
    }
}

task cppHeadersZip(type: Zip) {
    destinationDir = outputsFolder
    baseName = zipBaseName
    classifier = "headers"

    from(licenseFile) {
        into '/'
    }

    from('googletest/googletest/include') {
        into '/'
    }

    from('googletest/googlemock/include') {
        into '/'
    }
}

build.dependsOn cppHeadersZip
build.dependsOn cppSourcesZip

addTaskToCopyAllOutputs(cppHeadersZip)
addTaskToCopyAllOutputs(cppSourcesZip)

model {
    publishing {
        def gTaskList = createComponentZipTasks($.components, 'googletest', zipBaseName, Zip, project, includeStandardZipFormat)
        publications {
            cpp(MavenPublication) {
                gTaskList.each {
                    artifact it
                }
                artifact cppHeadersZip
                artifact cppSourcesZip

                artifactId = baseArtifactId
                groupId artifactGroupId
                version pubVersion
            }
        }
    }
}
